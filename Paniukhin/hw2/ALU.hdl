// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN
        x[16], y[16],  // 16-bit inputs
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:

    // Preprocessing
    Mux16(a=x, b=false, sel=zx, out=PREx);
    Mux16(a=y, b=false, sel=zy, out=PREy);

    Not16(in=PREx, out=NOTPREx);
    Not16(in=PREy, out=NOTPREy);

    Mux16(a=PREx, b=NOTPREx, sel=nx, out=INPUTx);
    Mux16(a=PREy, b=NOTPREy, sel=ny, out=INPUTy);

    // Executing either function
    And16(a=INPUTx, b=INPUTy, out=ANDxy);
    Add16(a=INPUTx, b=INPUTy, out=ADDxy);
    Mux16(a=ANDxy, b=ADDxy, sel=f, out=FUNC);

    // Calculating result
    Not16(in=FUNC, out=NOTFUNC);
    Mux16(a=FUNC, b=NOTFUNC, sel=no, out=out, out[0..7]=PREzr1, out[8..15]=PREzr2, out[15]=ng);

    // Calculating flags
    Or8Way(in=PREzr1, out=NOTzr1);
    Or8Way(in=PREzr2, out=NOTzr2);
    Or(a=NOTzr1, b=NOTzr2, out=notzr);
    Not(in=notzr, out=zr);
}